apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

ext {
    TARGET_SDK_VERSION = 27
    BUILD_TOOLS_VERSION = "27.0.3"

    // Available versions: https://developer.android.com/topic/libraries/support-library/revisions.html
    SUPPORT_LIBRARY_VERSION = "27.1.1"

    // https://dl.google.com/dl/android/maven2/index.html
    SUPPORT_TEST_VERSION = "1.0.2"
}

android {
    compileSdkVersion TARGET_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId 'net.kullo.android'
        minSdkVersion 19
        targetSdkVersion TARGET_SDK_VERSION
        versionCode 4800
        versionName "48"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }

    lintOptions {
        // See `lint --list` for available issue ids
        disable 'RtlCompat'    // Right-to-left text compatibility issues
        disable 'RtlHardcoded' // Using left/right instead of start/end attributes
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.apache.commons:commons-lang3:3.4'

    implementation "com.android.support:support-annotations:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:cardview-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:recyclerview-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:support-v4:${SUPPORT_LIBRARY_VERSION}"

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'com.isseiaoki:simplecropview:1.1.6'
    implementation 'net.danlew:android.joda:2.9.9'
    implementation "me.leolin:ShortcutBadger:1.1.19@aar"

    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.1'

    // Available versions:
    // ls -l $ANDROID_HOME/extras/google/m2repository/com/google/firebase/firebase-messaging
    // ls -l $ANDROID_HOME/extras/google/m2repository/com/google/android/gms/play-services-base
    implementation "com.google.firebase:firebase-messaging:10.2.6"
    implementation 'com.google.android.gms:play-services-base:10.2.6'

    implementation project(':libkullo')
    implementation project(':javautils')
    implementation project(':reduce-android')
    implementation project(':dialogs-for-android_core')

    testImplementation 'junit:junit:4.12'

    androidTestImplementation "com.android.support.test:runner:${SUPPORT_TEST_VERSION}"
    // Set this dependency to use JUnit 4 rules
    androidTestImplementation "com.android.support.test:rules:${SUPPORT_TEST_VERSION}"
    androidTestImplementation "com.android.support:support-annotations:${SUPPORT_LIBRARY_VERSION}"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

android {
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'

        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/x86/libc++_shared.so'
    }
}

apply plugin: 'com.google.gms.google-services'
