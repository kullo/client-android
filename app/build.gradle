apply plugin: 'com.android.application'

ext {
    TARGET_SDK_VERSION = 25
    BUILD_TOOLS_VERSION = "25.0.2"

    // Available versions: ls -l $ANDROID_HOME/extras/android/m2repository/com/android/support/design

    // There is a bug (?) with FloatingActionButton starting in 25.1.0
    // See https://code.google.com/p/android/issues/detail?id=230298
    SUPPORT_LIBRARY_VERSION = "25.3.1"
}

android {
    compileSdkVersion TARGET_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId 'net.kullo.android'
        minSdkVersion 16
        targetSdkVersion TARGET_SDK_VERSION
        versionCode 4403
        versionName "44"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }

    lintOptions {
        // See `lint --list` for available issue ids
        disable 'RtlCompat'    // Right-to-left text compatibility issues
        disable 'RtlHardcoded' // Using left/right instead of start/end attributes
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'org.apache.commons:commons-lang3:3.4'

    compile "com.android.support:support-annotations:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:cardview-v7:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:recyclerview-v7:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:support-v4:${SUPPORT_LIBRARY_VERSION}"

    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.isseiaoki:simplecropview:1.0.16'
    compile 'net.danlew:android.joda:2.9.2'
    compile "me.leolin:ShortcutBadger:1.1.13@aar"

    compile 'me.zhanghai.android.materialprogressbar:library:1.4.1'

    // Available versions: ls -l $ANDROID_HOME/extras/google/m2repository/com/google/android/gms/play-services-gcm
    compile "com.google.android.gms:play-services-gcm:9.8.0"

    compile project(':libkullo')
    compile project(':javautils')
    compile project(':reduce-android')
    compile project(':dialogs-for-android/core')

    testCompile 'junit:junit:4.12'
    // Available versions: ls -l $ANDROID_HOME/extras/android/m2repository/com/android/support/test/runner
    androidTestCompile 'com.android.support.test:runner:0.5'
    // Set this dependency to use JUnit 4 rules
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile "com.android.support:support-annotations:${SUPPORT_LIBRARY_VERSION}"
}

android {
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'

        pickFirst 'lib/arm64-v8a/libcrystax.so'
        pickFirst 'lib/armeabi-v7a/libcrystax.so'
        pickFirst 'lib/x86/libcrystax.so'
    }
}

apply plugin: 'com.google.gms.google-services'
